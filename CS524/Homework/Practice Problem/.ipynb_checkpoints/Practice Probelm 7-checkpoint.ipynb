{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "783a5983",
   "metadata": {},
   "source": [
    "## Logical Modeling practice\n",
    "\n",
    "Let’s suppose the CS524 class has decided collectivley to adopt some puppies. There are 6 different breeds of dogs we can adopt. Each breed has a minimum and maximum number of puppies if we adopt any of that breed (we have to adopt entire litters so no puppies are left alone!). These restrictions, along with the expected happiness we get from each breed, are in the following table:\n",
    "\n",
    "          Breed  Min adopted Max adopted Happiness\n",
    "    Golden retriever 5           7           8\n",
    "    Shiba Inu        2          10           10\n",
    "    Great Dane       3           5           4\n",
    "    Pomeranian       6          13           5\n",
    "    Water Spaniel    3          15           9\n",
    "    Husky            7          10           6\n",
    "    \n",
    "    \n",
    "We have decided that because of the personality of the Shiba Inu, the total number of Shiba Inu puppies adopted should be no more than the combined number of Golden Retriver, Water Spaniel, and Husky puppies. In addition, if any Pomeranians are adopted, we require that at least the minimum number of Great Dane puppies are adopted. We will adopt 40 total dogs and obviously we want to maximize our total happiness. Which breeds of puppies should we adopt, and how many of each?"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "ae85a957",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Dict{Symbol, Int64} with 6 entries:\n",
       "  :shiba      => 10\n",
       "  :greatdane  => 4\n",
       "  :golden     => 8\n",
       "  :husky      => 6\n",
       "  :spaniel    => 9\n",
       "  :pomeranian => 5"
      ]
     },
     "execution_count": 6,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "dogs = [:golden, :shiba, :greatdane, :pomeranian, :spaniel, :husky]\n",
    "\n",
    "min_adopt = Dict(zip(dogs, [5 2 3 6 3 7]))\n",
    "max_adopt = Dict(zip(dogs,[7 10 5 13 15 10]))\n",
    "happ = Dict(zip(dogs,[8 10 4 5 9 6]))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "5b6c1c2a",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Academic license - for non-commercial use only - expires 2022-07-06\n",
      "Adopt the following puppies: \n",
      "golden: 7.0\n",
      "shiba: 10.0\n",
      "spaniel: 15.0\n",
      "husky: 8.0\n"
     ]
    }
   ],
   "source": [
    "using JuMP, Gurobi\n",
    "\n",
    "m = Model(Gurobi.Optimizer)\n",
    "set_optimizer_attribute(m,\"OutputFlag\",0)\n",
    "\n",
    "# binary variable to determine whether we adopt any dog i\n",
    "@variable(m, z[dogs], Bin)\n",
    "# integer variable to determine how many of dog i\n",
    "@variable(m, x[dogs], Int)\n",
    "\n",
    "# maximize happiness\n",
    "@objective(m, Max, sum(happ[i] * x[i] for i in dogs))\n",
    "\n",
    "# adopt at least the minimum if we adopt any. \n",
    "# enforce the logic: x > 0 => x >= min\n",
    "@constraint(m, min_constr[i in dogs], x[i] >= min_adopt[i] * z[i])\n",
    "\n",
    "# adopt no more than the maximum if we adopt any.\n",
    "# enforce the logic: z = 0 => x = 0\n",
    "@constraint(m, max_constr[i in dogs], x[i] <= max_adopt[i] * z[i])\n",
    "\n",
    "# constraint on the total number of Shiba puppies\n",
    "# we can't adopt any shiba puppies\n",
    "# unless we adopt at elast that many of other dogs\n",
    "@constraint(m, x[:shiba] <= x[:golden] + x[:spaniel] + x[:husky])\n",
    "\n",
    "# constraint that z[:pom] = 1 => x[:greatdane] >= min\n",
    "@constraint(m, x[:greatdane] >= min_adopt[:greatdane] * z[:pomeranian])\n",
    "\n",
    "# adopt 40 dogs\n",
    "@constraint(m, sum(x) == 40)\n",
    "\n",
    "optimize!(m)\n",
    "\n",
    "println(\"Adopt the following puppies: \")\n",
    "\n",
    "for i in dogs\n",
    "    if value(z[i]) > 0.001\n",
    "        println(i, \": \", value(x[i]))\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e5fd5457",
   "metadata": {},
   "source": [
    "## Set Covering\n",
    "\n",
    "Thanos is here! Once again, Earth is relying on the Avengers for protection. For convenience,\n",
    "the United Nations has divided up the world into 8 different zones: North America (NA), South\n",
    "America (SA), Eurasia (EU), North Africa (NF), South Africa (SF), East Asia (EA), South/West\n",
    "Asia (WA), and Oceania (OC). The number of hours required to get from each zone to each other\n",
    "zone by Quinjet is given in the following table:\n",
    "\n",
    "\n",
    "    Zone NA SA EU NF SF EA WA OC\n",
    "     NA  0  5  7  8  10 15 11 16\n",
    "     SA  5  0 11 10   9 15 12 13\n",
    "     EU  7 11  0  4   7  9  5 19\n",
    "     NF  8 10  4  0   3  8  3 18\n",
    "     SF 10  9  7  3   0 10  4 16\n",
    "     EA 15 15  9  8  10  0  3  7\n",
    "     WA 11 12  5  3   4  3  0 15\n",
    "     OC 16 13 19 18  16  7 15  0\n",
    "\n",
    "\n",
    "A rough estimate of the population in each district (in millions) as follows: NA: 580, SA: 420, EU:\n",
    "900, NF: 250, SF: 200, EA: 1700, WA: 400, OC: 40. The UN have declared there are going to be n\n",
    "Avenger locations. The problem is, the UN isn’t sure how many of the Avengers will be available\n",
    "during the defense against Thanos. Their goal is to maximize the number of people that live within\n",
    "3 hours of an Avenger. We have been asked to assist the UN by determining where to place the\n",
    "Avengers for different values of n. Solve the problem for n = 1, 2, 3, 4."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "id": "a8429a2c",
   "metadata": {},
   "outputs": [],
   "source": [
    "n = 1\n",
    "\n",
    "locs = [:NA, :SA, :EU, :NF, :SF, :EA, :WA, :OC]\n",
    "\n",
    "using NamedArrays\n",
    "\n",
    "trav_time = NamedArray(\n",
    "[\n",
    "0 5 7 8 10 15 11 16\n",
    "5 0 11 10 9 15 12 13\n",
    "7 11 0 4 7 9 5 19\n",
    "8 10 4 0 3 8 3 18\n",
    "10 9 7 3 0 10 4 16\n",
    "15 15 9 8 10 0 3 7\n",
    "11 12 5 3 4 3 0 15\n",
    "16 13 19 18 16 7 15 0], (locs, locs), (\"locs\", \"locs\"))\n",
    "\n",
    "pop = Dict(zip(locs,[580 420 900 250 200 1700 400 40]))\n",
    "\n",
    "cover = NamedArray(zeros(length(locs), length(locs)), (locs, locs), (\"locs\", \"locs\"))\n",
    "\n",
    "for i in locs\n",
    "    for j in locs\n",
    "        if trav_time[i,j] <= 3\n",
    "            cover[i,j] = 1\n",
    "        end\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 23,
   "id": "2f57a559",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Academic license - for non-commercial use only - expires 2022-07-06\r\n",
      "The number of Avenger(s): 1\n",
      "Place Avenger in distrcit WA\n",
      "The following districts will be covered: \n",
      "NF\n",
      "\n",
      "EA\n",
      "\n",
      "WA\n",
      "\n",
      "Academic license - for non-commercial use only - expires 2022-07-06\r\n",
      "The number of Avenger(s): 2\n",
      "Place Avenger in distrcit EU\n",
      "The following districts will be covered: \n",
      "EU\n",
      "\n",
      "NF\n",
      "\n",
      "EA\n",
      "\n",
      "WA\n",
      "\n",
      "Place Avenger in distrcit WA\n",
      "The following districts will be covered: \n",
      "EU\n",
      "\n",
      "NF\n",
      "\n",
      "EA\n",
      "\n",
      "WA\n",
      "\n",
      "Academic license - for non-commercial use only - expires 2022-07-06\r\n",
      "The number of Avenger(s): 3\n",
      "Place Avenger in distrcit NA\n",
      "The following districts will be covered: \n",
      "NA\n",
      "\n",
      "EU\n",
      "\n",
      "NF\n",
      "\n",
      "EA\n",
      "\n",
      "WA\n",
      "\n",
      "Place Avenger in distrcit EU\n",
      "The following districts will be covered: \n",
      "NA\n",
      "\n",
      "EU\n",
      "\n",
      "NF\n",
      "\n",
      "EA\n",
      "\n",
      "WA\n",
      "\n",
      "Place Avenger in distrcit WA\n",
      "The following districts will be covered: \n",
      "NA\n",
      "\n",
      "EU\n",
      "\n",
      "NF\n",
      "\n",
      "EA\n",
      "\n",
      "WA\n",
      "\n",
      "Academic license - for non-commercial use only - expires 2022-07-06\r\n",
      "The number of Avenger(s): 4\n",
      "Place Avenger in distrcit NA\n",
      "The following districts will be covered: \n",
      "NA\n",
      "\n",
      "SA\n",
      "\n",
      "EU\n",
      "\n",
      "NF\n",
      "\n",
      "EA\n",
      "\n",
      "WA\n",
      "\n",
      "Place Avenger in distrcit SA\n",
      "The following districts will be covered: \n",
      "NA\n",
      "\n",
      "SA\n",
      "\n",
      "EU\n",
      "\n",
      "NF\n",
      "\n",
      "EA\n",
      "\n",
      "WA\n",
      "\n",
      "Place Avenger in distrcit EU\n",
      "The following districts will be covered: \n",
      "NA\n",
      "\n",
      "SA\n",
      "\n",
      "EU\n",
      "\n",
      "NF\n",
      "\n",
      "EA\n",
      "\n",
      "WA\n",
      "\n",
      "Place Avenger in distrcit WA\n",
      "The following districts will be covered: \n",
      "NA\n",
      "\n",
      "SA\n",
      "\n",
      "EU\n",
      "\n",
      "NF\n",
      "\n",
      "EA\n",
      "\n",
      "WA\n",
      "\n"
     ]
    }
   ],
   "source": [
    "using JuMP, Gurobi\n",
    "# for each possible number of available Avengers\n",
    "for n in [1,2,3,4]\n",
    "    \n",
    "    m = Model(Gurobi.Optimizer)\n",
    "    set_optimizer_attribute(m,\"OutputFlag\",0)\n",
    "    \n",
    "    # binary variable representing whether we put an Avenger in location i \\in␣locs\n",
    "    @variable(m, x[locs], Bin)\n",
    "    \n",
    "    # binary variable representing whether location i in locs is covered\n",
    "    @variable(m, c[locs], Bin)\n",
    "    \n",
    "    # maximize the population covered\n",
    "    @objective(m, Max, sum(pop[j]*c[j] for j in locs))\n",
    "    \n",
    "    # constraint that the number of locations we place Avengers = n\n",
    "    @constraint(m, sum(x) == n)\n",
    "    \n",
    "    # cover constraint. If loc j is covered by loc i, this will be 1*x[i] >= c[j]\n",
    "    # this also enforces logic that if we want c[j] = 1, at least one x[i] that covers loc j must be 1\n",
    "    @constraint(m, cov_con[j in locs], sum(cover[i,j] * x[i] for i in locs) >= c[j])\n",
    "    \n",
    "    optimize!(m)\n",
    "    \n",
    "    println(\"The number of Avenger(s): \", n)\n",
    "    \n",
    "    for i in locs\n",
    "        if value(x[i]) == 1\n",
    "            println(\"Place Avenger in distrcit \", i)\n",
    "            println(\"The following districts will be covered: \" )\n",
    "            for j in locs\n",
    "                if value(c[j]) == 1\n",
    "                    println(j)\n",
    "        println()\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4ce75dbf",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.6.1",
   "language": "julia",
   "name": "julia-1.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
