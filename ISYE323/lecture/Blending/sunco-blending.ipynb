{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Maximum profit: $287750.0\n",
      "production plan: \n",
      "Total of Gas 1 to sell: 3000.0\n",
      "\t Mixed from:\n",
      "\t \t Crude 1: 2000.0\n",
      "\t \t Crude 2: 1000.0\n",
      "\t \t Crude 3: 0.0\n",
      "\t Advertising for gas 1: 0.0\n",
      "Total of Gas 2 to sell: 9500.0\n",
      "\t Mixed from:\n",
      "\t \t Crude 1: 2333.3\n",
      "\t \t Crude 2: 3666.7\n",
      "\t \t Crude 3: 3500.0\n",
      "\t Advertising for gas 2: 750.0\n",
      "Total of Gas 3 to sell: 1000.0\n",
      "\t Mixed from:\n",
      "\t \t Crude 1: 666.7\n",
      "\t \t Crude 2: 333.3\n",
      "\t \t Crude 3: 0.0\n",
      "\t Advertising for gas 3: 0.0\n",
      "Presolve 13 (-3) rows, 15 (-3) columns and 51 (-6) elements\n",
      "0  Obj 380000 Primal inf 10333.333 (6) Dual inf 179.7 (3)\n",
      "12  Obj 287750\n",
      "Optimal - objective value 287750\n",
      "After Postsolve, objective 287750, infeasibilities - dual 0 (0), primal 0 (0)\n",
      "Optimal objective 287750 - 12 iterations time 0.002, Presolve 0.00\n"
     ]
    }
   ],
   "source": [
    "using JuMP, Cbc\n",
    "\n",
    "# sets\n",
    "gases=1:3\n",
    "crudes=1:3\n",
    "\n",
    "#data\n",
    "gasprice=[70,60,50]\n",
    "minoctane=[10,8,6]\n",
    "maxsulfer=[1,2,1]\n",
    "mingas=[3000,2000,1000]\n",
    "# added the $4 processing cost to crude cost\n",
    "crudecost=[49,39,29]\n",
    "crudeoct=[12,6,8]\n",
    "crudesulf=[0.5,2,3]\n",
    "maxcrude=[5000,5000,5000]\n",
    "maxtotal=14000\n",
    "adbpd=10  # advertising barrels per dollar\n",
    "\n",
    "# model\n",
    "m = Model()\n",
    "\n",
    "@variable(m, blend[crudes,gases] >= 0)\n",
    "@variable(m, 0 <= crude_buy[i in crudes] <= maxcrude[i])\n",
    "@variable(m, gas_sell[gases] >= 0)\n",
    "@variable(m, adspend[gases] >= 0)\n",
    "\n",
    "@objective(m, Max,\n",
    "    sum(gasprice[j]*gas_sell[j] for j in gases)\n",
    "    - sum(crudecost[i]*crude_buy[i] for i in crudes)\n",
    "    - sum(adspend[j] for j in gases))\n",
    "\n",
    "@constraint(m, gastot[j in gases],\n",
    "    sum(blend[i,j] for i in crudes) == gas_sell[j])\n",
    "@constraint(m, crudetot[i in crudes],\n",
    "    sum(blend[i,j] for j in gases) == crude_buy[i])\n",
    "@constraint(m, maxproc,\n",
    "    sum(crude_buy[i] for i in crudes) <= maxtotal)\n",
    "@constraint(m, meetdemand[j in gases],\n",
    "    gas_sell[j] == mingas[j]+adbpd*adspend[j])\n",
    "@constraint(m, octanelb[j in gases],\n",
    "    sum(crudeoct[i]*blend[i,j] for i in crudes)\n",
    "        >= minoctane[j]*gas_sell[j])\n",
    "@constraint(m, sulfurub[j in gases],\n",
    "    sum(crudesulf[i]*blend[i,j] for i in crudes)\n",
    "        <= maxsulfer[j]*gas_sell[j])\n",
    "\n",
    "set_optimizer(m,Cbc.Optimizer)\n",
    "optimize!(m)\n",
    "\n",
    "# print total cost\n",
    "println(\"Maximum profit: \\$\", objective_value(m))\n",
    "\n",
    "println(\"production plan: \")\n",
    "for j in gases\n",
    "    println(\"Total of Gas \", j, \" to sell: \", round(value(gas_sell[j]),digits=1))\n",
    "    println(\"\\t Mixed from:\")\n",
    "    for i in crudes\n",
    "        println(\"\\t \\t Crude \", i, \": \", round(value(blend[i,j]),digits=1))\n",
    "    end\n",
    "    println(\"\\t Advertising for gas \", j, \": \", round(value(adspend[j]),digits=1))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.1",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
